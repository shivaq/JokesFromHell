apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{
            debuggable true
            shrinkResources false
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions("stage", "mode")

    productFlavors{
        dev{
            dimension "stage"
            minSdkVersion 21
            applicationIdSuffix ".dev"
        }
        prod{
            dimension "stage"
        }
        free{
            dimension "mode"
            applicationIdSuffix ".free"
        }
        paid{
            dimension "mode"
            applicationIdSuffix ".paid"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // android support libraries
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"

    // butterknife
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // Logger
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // dependency injection
    compile "com.google.dagger:dagger:$rootProject.dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'javax.inject:javax.inject:1'

    // unit test
    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.assertj:assertj-core:$rootProject.assertJ"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"

    // ui test
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestCompile "com.google.dexmaker:dexmaker:$rootProject.dexmakerVersion"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:$rootProject.dexmakerVersion"
    androidTestCompile("com.squareup.assertj:assertj-android:$rootProject.squareAssertJ", {
        exclude module: 'support-annotations'
    })

    // google play services
    freeCompile "com.google.android.gms:play-services-gcm:$rootProject.googlePlayServicesVersion"
    freeCompile "com.google.android.gms:play-services-ads:$rootProject.googlePlayServicesVersion"

    // java local library
    compile project(':jokesinhell')
    // android library
    compile project(':jokedisplay')
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile project(path: ':backend', configuration: 'android-endpoints')
}
